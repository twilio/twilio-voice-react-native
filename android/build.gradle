
buildscript {
    ext.versions = [
      'androidGradlePlugin': '4.1.2',
      'googleServices'     : '4.3.8',
      'voiceAndroid'       : '5.7.2',
      'androidxLifecycle'  : '2.2.0',
      'firebaseMessaging'  : '21.1.0'
    ]
    if (project == rootProject) {
        repositories {
            google()
            jcenter()
        }

        dependencies {
            classpath "com.android.tools.build:gradle:${versions.androidGradlePlugin}"
            classpath "com.google.gms:google-services:${versions.googleServices}"
        }
    }
}

apply plugin: 'com.android.library'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('TwilioVoiceReactNative_compileSdkVersion', 29)
    buildToolsVersion safeExtGet('TwilioVoiceReactNative_buildToolsVersion', '29.0.2')
    defaultConfig {
        minSdkVersion safeExtGet('TwilioVoiceReactNative_minSdkVersion', 16)
        targetSdkVersion safeExtGet('TwilioVoiceReactNative_targetSdkVersion', 29)
        versionCode 1
        versionName "1.0"

    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    lintOptions {
        disable 'GradleCompatible'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
  google()
  mavenCentral()
  jcenter()
  maven {
    url 'https://maven.google.com/'
    name 'Google'
  }
}

dependencies {
    //noinspection GradleDynamicVersion
    implementation "com.facebook.react:react-native:+"  // From node_modules
    implementation "com.twilio:voice-android:${versions.voiceAndroid}"
    implementation "androidx.lifecycle:lifecycle-extensions:${versions.androidxLifecycle}"
    implementation "com.google.firebase:firebase-messaging:${versions.firebaseMessaging}"
    implementation "com.twilio:audioswitch:1.1.2"
    implementation 'com.google.android.material:material:1.1.0'
}
